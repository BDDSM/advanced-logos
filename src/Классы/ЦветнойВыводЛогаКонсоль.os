#Использовать logos
//////////////////////////////////////////////////////////////////////////
//
// LOGOS: цветной вывод в консоль
//
//////////////////////////////////////////////////////////////////////////

Перем КартаУровней;
Перем КонсольВывода;
Перем ВывестиВЦвете;
Перем ЦветоваяСхема;
Перем ИспользоватьПрефиксы;
Перем ФорматДатыСобытия;
Перем ЦветТекстаКонсоли;

Процедура ВывестиСобытие(Знач СобытиеЛога) Экспорт
	
	Сообщение = СобытиеЛога.ПолучитьСообщение();
	УровеньСообщения = СобытиеЛога.ПолучитьУровень();
	ДатаСобытия = Дата("00010101") + (СобытиеЛога.ПолучитьВремяСобытия() / 1000);
	ДопПоля = СобытиеЛога.ПолучитьДополнительныеПоля();
	Префикс = ПолучитьПрефикс(ДопПоля);

	Если ВывестиВЦвете Тогда
	
		ЦветнойВывод(ДатаСобытия, УровеньСообщения, Префикс, Сообщение, ДопПоля);

	Иначе
		
		ПростойВывод(ДатаСобытия, УровеньСообщения, Префикс, Сообщение, ДопПоля);

	КонецЕсли;

КонецПроцедуры

Процедура Закрыть() Экспорт

	КонсольВывода = Неопределено;

КонецПроцедуры

Процедура ОтключитьЦвета() Экспорт
	ВывестиВЦвете = Ложь;
КонецПроцедуры

Процедура ОтключитьПрефиксы() Экспорт
	ИспользоватьПрефиксы = Ложь;
КонецПроцедуры

Процедура УстановитьЦветовуюСхему(Знач НоваяЦветоваяСхема) Экспорт
	ЦветоваяСхема = НоваяЦветоваяСхема;
КонецПроцедуры

Процедура УстановитьКартуУровней(Знач НоваяКартаУровней) Экспорт
	КартаУровней = НоваяКартаУровней;
КонецПроцедуры

Процедура УстановитьФорматДатыСобытия(Знач ФорматДаты)
	ФорматДатыСобытия = СтрШаблон("ДФ='%1'", ФорматДаты);
КонецПроцедуры


// Устанавливает свойство аппендера, заданное в конфигурационном файле
//
Процедура УстановитьСвойство(Знач ИмяСвойства, Знач Значение) Экспорт
	
КонецПроцедуры // УстановитьСвойство()

Функция ЦветоваяСхемаПоУмолчанию() Экспорт
	
	КартаСхемы = Новый Соответствие();
	КартаСхемы.Вставить(УровниЛога.Отладка, ЦветКонсоли.Синий);
	КартаСхемы.Вставить(УровниЛога.Информация, ЦветКонсоли.Зеленый);
	КартаСхемы.Вставить(УровниЛога.Предупреждение, ЦветКонсоли.Желтый);
	КартаСхемы.Вставить(УровниЛога.Ошибка, ЦветКонсоли.Красный);
	КартаСхемы.Вставить(УровниЛога.КритичнаяОшибка, ЦветКонсоли.Красный);
	КартаСхемы.Вставить("date", ЦветКонсоли.Малиновый);
	КартаСхемы.Вставить("prefix", ЦветКонсоли.Бирюза);

	Возврат КартаСхемы;

КонецФункции

Функция КартаУровнейПоУмолчанию() Экспорт
	
	КартаСтатусовИУровней = Новый Соответствие;
	КартаСтатусовИУровней.Вставить(УровниЛога.Отладка, "DEBUG");
	КартаСтатусовИУровней.Вставить(УровниЛога.Информация, " INFO");
	КартаСтатусовИУровней.Вставить(УровниЛога.Предупреждение, " WARN");
	КартаСтатусовИУровней.Вставить(УровниЛога.Ошибка, "PANIC");
	КартаСтатусовИУровней.Вставить(УровниЛога.КритичнаяОшибка, "FATAL");

	Возврат КартаСтатусовИУровней;

КонецФункции

Процедура ЦветнойВывод(Знач ДатаСобытия, Знач УровеньСообщения, Знач Префикс, Знач Сообщение, Знач ДопПоля)
	
	ЦветТекстаКонсоли = КонсольВывода.ЦветТекста;

	ВывестиДатуСобытия(ДатаСобытия);
	ВывестиУровень(УровеньСообщения);
	ВывестиПрефикс(Префикс);
	ВывестиСообщение(Сообщение);
	ВывестиПоля(ДопПоля, УровеньСообщения);

	УстановитьЦветТекстаПоУмолчанию();
	КонсольВывода.Вывести(Символы.ПС);
КонецПроцедуры

Процедура ПростойВывод(Знач ДатаСобытия, Знач УровеньСообщения, Знач Префикс, Знач Сообщение, Знач ДопПоля)
	
	ВывестиДатуСобытия(ДатаСобытия);
	ВывестиУровень(УровеньСообщения);
	ВывестиПрефикс(Префикс);
	ВывестиСообщение(Сообщение);
	ВывестиПоля(ДопПоля, УровеньСообщения);

КонецПроцедуры

Процедура ВывестиУровень(Знач УровеньСообщения)

	УстановитьЦветТекста(ЦветоваяСхема[УровеньСообщения]);
	КонсольВывода.Вывести(КартаУровней[УровеньСообщения] + " ");

КонецПроцедуры

Процедура ВывестиСообщение(Знач ТекстСообщения)

	УстановитьЦветТекстаПоУмолчанию();
	КонсольВывода.Вывести(ТекстСообщения);

КонецПроцедуры

Процедура ВывестиПоля(Знач Поля, Знач УровеньСообщения)

	Для каждого Поле Из Поля Цикл
		
		ИмяПоля = Поле.Ключ;

		Если ЭтоПолеПрефикса(ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеПоля = Поле.Значение;
		КонсольВывода.Вывести(" ");
		УстановитьЦветТекста(ЦветоваяСхема[УровеньСообщения]);
		КонсольВывода.Вывести(ИмяПоля);
		УстановитьЦветТекстаПоУмолчанию();
		КонсольВывода.Вывести(СтрШаблон("=%1", ЗначениеПоля));
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоПолеПрефикса(Знач ИмяПоля)
	
	Возврат НРег(ИмяПоля) = "prefix"
		ИЛИ НРег(ИмяПоля) = "префикс"
		ИЛИ НРег(ИмяПоля) = "модуль";

КонецФункции

Функция ПолучитьПрефикс(Знач Поля)
	
	Если НЕ ИспользоватьПрефиксы Тогда
		Возврат "";
	КонецЕсли;

	Префикс = "main";
	
	Для каждого Поле Из Поля Цикл
		ИмяПоля = Поле.Ключ;

		Если ЭтоПолеПрефикса(ИмяПоля) Тогда
			Префикс = Поле.Значение;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;

	Возврат Префикс;

КонецФункции

Процедура ВывестиДатуСобытия(Знач ДатаСобытия)

	УстановитьЦветТекста(ЦветоваяСхема["date"]);
	ФорматированнаяДата = Формат(ДатаСобытия, ФорматДатыСобытия);
	ТекстДатыСобытия = СтрШаблон("[%1] ", ФорматированнаяДата);
	КонсольВывода.Вывести(ТекстДатыСобытия);

КонецПроцедуры

Процедура ВывестиПрефикс(Знач Префикс)

	Если НЕ ИспользоватьПрефиксы Тогда
		Возврат;
	КонецЕсли;

	Если ПустаяСтрока(Префикс) Тогда
		Префикс = "main";
	КонецЕсли;

	УстановитьЦветТекста(ЦветоваяСхема["prefix"]);

	КонсольВывода.Вывести(Префикс + ": ");

КонецПроцедуры

Процедура УстановитьЦветТекста(Знач ЦветТекста)
	
	КонсольВывода.ЦветТекста = ЦветТекста;
	
КонецПроцедуры

Процедура УстановитьЦветТекстаПоУмолчанию()
	
	КонсольВывода.ЦветТекста = ЦветТекстаКонсоли;
	
КонецПроцедуры

Процедура ПриСозданииОбъекта()
	
	КонсольВывода = Новый Консоль();
	ИспользоватьПрефиксы = Истина;
	ВывестиВЦвете = Истина;
	
	УстановитьЦветовуюСхему(ЦветоваяСхемаПоУмолчанию());
	УстановитьКартуУровней(КартаУровнейПоУмолчанию());
	УстановитьФорматДатыСобытия("yyyy/MM/dd hh:mm:ss");
	
КонецПроцедуры

