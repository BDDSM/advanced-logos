#Использовать logos
//////////////////////////////////////////////////////////////////////////
//
// LOGOS: цветной вывод в консоль
//
//////////////////////////////////////////////////////////////////////////

Перем КартаУровней;
Перем КонсольВывода;
Перем ВывестиВЦвете;
Перем ЦветоваяСхема;
Перем ИспользоватьПрефиксы;
Перем ВыводитьДату;
Перем ФорматДатыСобытия;
Перем ЦветТекстаКонсоли;

#Область Интерфейса_аппендера_логоса

// Выводит событие лога 
//
// Параметры:
//   СобытиеЛога - Объект - объект класса <СобытиеЛога>
//
Процедура ВывестиСобытие(Знач СобытиеЛога) Экспорт
	
	Сообщение = СобытиеЛога.ПолучитьСообщение();
	УровеньСообщения = СобытиеЛога.ПолучитьУровень();
	ДатаСобытия = Дата("00010101") + (СобытиеЛога.ПолучитьВремяСобытия() / 1000);
	ДопПоля = СобытиеЛога.ПолучитьДополнительныеПоля();
	Префикс = ПолучитьПрефикс(СобытиеЛога.ПолучитьИмяЛога(), ДопПоля);

	ФорматированнаяДатыСобытия = ФорматироватьДатуСобытия(ДатаСобытия);
	ФорматированныйУровеньСообщения = ФорматироватьУровеньСообщения(УровеньСообщения);
	
	Если ВывестиВЦвете Тогда
	
		ЦветнойВывод(ФорматированнаяДатыСобытия, ФорматированныйУровеньСообщения, УровеньСообщения, Префикс, Сообщение, ДопПоля);

	Иначе
		
		ПростойВывод(ФорматированнаяДатыСобытия, ФорматированныйУровеньСообщения, Префикс, Сообщение, ДопПоля);

	КонецЕсли;

КонецПроцедуры

// Устанавливает свойство аппендера, заданное в конфигурационном файле
//
Процедура УстановитьСвойство(Знач ИмяСвойства, Знач Значение) Экспорт
	
КонецПроцедуры // УстановитьСвойство()

Процедура Закрыть() Экспорт

КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Отключение вывода сообщения события в цвете
//
Процедура ОтключитьЦвета() Экспорт
	ВывестиВЦвете = Ложь;
КонецПроцедуры

// Отключение вывода даты сообщения события
//
Процедура ОтключитьДатуСобытия() Экспорт
	ВыводитьДату = Ложь;
КонецПроцедуры

// Отключение использования префиксов
//
Процедура ОтключитьПрефиксы() Экспорт
	ИспользоватьПрефиксы = Ложь;
КонецПроцедуры

// Устанавливает карту уровней сообщений на русском языке
//
// УровниЛога.Отладка    		= "   ОТЛАДКА"
// УровниЛога.Информация 		= "ИНФОРМАЦИЯ"
// УровниЛога.Предупреждение 	= "  ВНИМАНИЕ"
// УровниЛога.Ошибка			- "    ОШИБКА"
// УровниЛога.КритичнаяОшибка 	= " КРИТИЧНАЯ"
//
Процедура УровниСообщенияНаРусском() Экспорт
	УстановитьКартуУровней(КартаУровнейНаРусском());
КонецПроцедуры

// Устанавливает карту уровней сообщений по умолчанию 
//
// УровниЛога.Отладка    		= "DEBUG"
// УровниЛога.Информация 		= " INFO"
// УровниЛога.Предупреждение 	= " WARN"
// УровниЛога.Ошибка			- "ERROR"
// УровниЛога.КритичнаяОшибка 	= "FATAL"
//
Процедура УровниСообщенияПоУмолчанию() Экспорт
	УстановитьКартуУровней(КартаУровнейПоУмолчанию());
КонецПроцедуры

// Устанавливает новую схему раскраски консоли
//
// Параметры:
//   НоваяЦветоваяСхема - Соответствие - можно задавать несколько ключей, все заполнять не обязательно
// 	                                     состоит из:
//                                       * УровниЛога (из перечисления УровниЛога)
//                                       * Префикс
//                                       * ДатаСобытия
//
Процедура УстановитьЦветовуюСхему(Знач НоваяЦветоваяСхема) Экспорт
	
	Для каждого КлючЗначение Из НоваяЦветоваяСхема Цикл
		ЦветоваяСхема.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;

КонецПроцедуры

// Устанавливает произвольную карту уровней сообщений
//
// Параметры:
//   НоваяКартаУровней - Соответствие - карта соответствия уровней лога их названиям
//
Процедура УстановитьКартуУровней(Знач НоваяКартаУровней) Экспорт
	
	Для каждого КлючЗначение Из НоваяКартаУровней Цикл
		КартаУровней.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;

КонецПроцедуры

// Устанавливает произвольный формат вывода даты
//
// Параметры:
//   ФорматДаты - Строка - строковое представление формата для вывода даты
//
Процедура УстановитьФорматДатыСобытия(Знач ФорматДаты) Экспорт
	ФорматДатыСобытия = СтрШаблон("ДФ='%1'", ФорматДаты);
КонецПроцедуры

#КонецОбласти

#Область Вывод_сообщения

Процедура ЦветнойВывод(Знач ДатаСобытия, Знач ФорматированныйУровеньСообщения, Знач УровеньСообщения, Знач Префикс, Знач Сообщение, Знач ДопПоля)
	
	ЦветТекстаКонсоли = КонсольВывода.ЦветТекста;

	ЦветУровняСообщения =  ЦветоваяСхема[УровеньСообщения];

	Если ВыводитьДату Тогда
		ВывестиДатуСобытия(ДатаСобытия);		
	КонецЕсли;

	ВывестиУровень(ФорматированныйУровеньСообщения, ЦветУровняСообщения);

	Если ИспользоватьПрефиксы Тогда
		ВывестиПрефикс(Префикс);		
	КонецЕсли;

	ВывестиСообщение(Сообщение);
	ВывестиПоля(ДопПоля, ЦветУровняСообщения);

	УстановитьЦветТекстаПоУмолчанию();
	КонсольВывода.Вывести(Символы.ПС);

КонецПроцедуры

Процедура ПростойВывод(Знач ФорматированнаяДатаСобытия, Знач ФорматированныйУровеньСообщения, Знач Префикс, Знач Сообщение, Знач ДопПоля)
	
	МассивСтрокВывода = Новый Массив();

	Если ВыводитьДату Тогда
		МассивСтрокВывода.Добавить(ФорматированнаяДатаСобытия);
	КонецЕсли;
	
	МассивСтрокВывода.Добавить(ФорматированныйУровеньСообщения);
	
	Если ИспользоватьПрефиксы Тогда
		МассивСтрокВывода.Добавить(СтрШаблон("%1:", Префикс));		
	КонецЕсли;

	МассивСтрокВывода.Добавить(Сообщение);

	Для каждого Поле Из ДопПоля Цикл
		
		ИмяПоля = Поле.Ключ;

		Если ЭтоПолеПрефикса(ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеПоля = Поле.Значение;
		СтрокаПоля = СтрШаблон("%1=%2", ИмяПоля, ЗначениеПоля);
		
		МассивСтрокВывода.Добавить(СтрокаПоля);

	КонецЦикла;

	СообщениеВывода = СтрСоединить(МассивСтрокВывода, " ");	

	Сообщить(СообщениеВывода);

КонецПроцедуры

#КонецОбласти

#Область Цветной_вывод_в_консоль

Процедура ВывестиУровень(Знач ФорматированныйУровень, Знач ЦветУровняСообщения)

	УстановитьЦветТекста(ЦветУровняСообщения);
	КонсольВывода.Вывести(ФорматированныйУровень + " ");

КонецПроцедуры

Процедура ВывестиСообщение(Знач ТекстСообщения)

	УстановитьЦветТекстаПоУмолчанию();
	КонсольВывода.Вывести(ТекстСообщения);

КонецПроцедуры

Процедура ВывестиПоля(Знач Поля, Знач ЦветУровняСообщения)

	Для каждого Поле Из Поля Цикл
		
		ИмяПоля = Поле.Ключ;

		Если ЭтоПолеПрефикса(ИмяПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		ЗначениеПоля = Поле.Значение;
		КонсольВывода.Вывести(" ");
		УстановитьЦветТекста(ЦветУровняСообщения);
		КонсольВывода.Вывести(ИмяПоля);
		УстановитьЦветТекстаПоУмолчанию();
		КонсольВывода.Вывести(СтрШаблон("=%1", ЗначениеПоля));
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиДатуСобытия(Знач ДатаСобытия)

	УстановитьЦветТекста(ЦветоваяСхема["ДатаСобытия"]);
	КонсольВывода.Вывести(ДатаСобытия + " ");

КонецПроцедуры

Процедура ВывестиПрефикс(Знач Префикс)

	Если НЕ ИспользоватьПрефиксы Тогда
		Возврат;
	КонецЕсли;

	Если ПустаяСтрока(Префикс) Тогда
		Префикс = "main";
	КонецЕсли;

	УстановитьЦветТекста(ЦветоваяСхема["Префикс"]);

	КонсольВывода.Вывести(Префикс + ": ");

КонецПроцедуры

Процедура УстановитьЦветТекста(Знач ЦветТекста)
	
	КонсольВывода.ЦветТекста = ЦветТекста;
	
КонецПроцедуры

Процедура УстановитьЦветТекстаПоУмолчанию()
	
	КонсольВывода.ЦветТекста = ЦветТекстаКонсоли;
	
КонецПроцедуры

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

Функция ЭтоПолеПрефикса(Знач ИмяПоля)
	
	Возврат НРег(ИмяПоля) = "prefix"
		ИЛИ НРег(ИмяПоля) = "префикс"
		ИЛИ НРег(ИмяПоля) = "модуль";

КонецФункции

Функция ПолучитьПрефикс(Знач ИмяЛога, Знач Поля)
	
	Если НЕ ИспользоватьПрефиксы Тогда
		Возврат "";
	КонецЕсли;

	Префикс = ПрефиксИзИмениЛога(ИмяЛога);
	
	Для каждого Поле Из Поля Цикл
		ИмяПоля = Поле.Ключ;

		Если ЭтоПолеПрефикса(ИмяПоля) Тогда
			Префикс = Поле.Значение;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;

	Возврат Префикс;

КонецФункции

Функция ПрефиксИзИмениЛога(Знач ИмяЛога)

	МассивИменЛога = СтрРазделить(ИмяЛога, ".");

	Если МассивИменЛога.Количество() > 3 Тогда

		Возврат МассивИменЛога[2];

	Иначе	

		Возврат МассивИменЛога[МассивИменЛога.ВГраница()];

	КонецЕсли;
	
КонецФункции

Функция ФорматироватьДатуСобытия(Знач ДатаСобытия)
	Возврат Формат(ДатаСобытия, ФорматДатыСобытия);
КонецФункции

Функция ФорматироватьУровеньСообщения(Знач УровеньСообщения)
	Возврат КартаУровней[УровеньСообщения];
КонецФункции

Функция ЦветоваяСхемаПоУмолчанию()
	
	КартаСхемы = Новый Соответствие();
	КартаСхемы.Вставить(УровниЛога.Отладка, 		ЦветКонсоли.Синий);
	КартаСхемы.Вставить(УровниЛога.Информация, 		ЦветКонсоли.Зеленый);
	КартаСхемы.Вставить(УровниЛога.Предупреждение, 	ЦветКонсоли.Желтый);
	КартаСхемы.Вставить(УровниЛога.Ошибка, 			ЦветКонсоли.Красный);
	КартаСхемы.Вставить(УровниЛога.КритичнаяОшибка, ЦветКонсоли.Красный);
	КартаСхемы.Вставить("ДатаСобытия", 				ЦветКонсоли.Малиновый);
	КартаСхемы.Вставить("Префикс", 					ЦветКонсоли.Бирюза);

	Возврат КартаСхемы;

КонецФункции

Функция КартаУровнейПоУмолчанию()
	
	КартаСтатусовИУровней = Новый Соответствие;
	КартаСтатусовИУровней.Вставить(УровниЛога.Отладка, 			"DEBUG");//  ОТЛАДКА
	КартаСтатусовИУровней.Вставить(УровниЛога.Информация, 		" INFO");//     ИНФО
	КартаСтатусовИУровней.Вставить(УровниЛога.Предупреждение,  	" WARN");// ВНИМАНИЕ 
	КартаСтатусовИУровней.Вставить(УровниЛога.Ошибка, 		   	"ERROR");//   ОШИБКА
	КартаСтатусовИУровней.Вставить(УровниЛога.КритичнаяОшибка, 	"FATAL");// КРИТИЧНА

	Возврат КартаСтатусовИУровней;

КонецФункции

Функция КартаУровнейНаРусском()
	
	КартаСтатусовИУровней = Новый Соответствие;
	КартаСтатусовИУровней.Вставить(УровниЛога.Отладка, 			"   ОТЛАДКА");//  ОТЛАДКА
	КартаСтатусовИУровней.Вставить(УровниЛога.Информация, 		"ИНФОРМАЦИЯ");//     ИНФО
	КартаСтатусовИУровней.Вставить(УровниЛога.Предупреждение, 	"  ВНИМАНИЕ");// ВНИМАНИЕ 
	КартаСтатусовИУровней.Вставить(УровниЛога.Ошибка, 			"    ОШИБКА");//   ОШИБКА
	КартаСтатусовИУровней.Вставить(УровниЛога.КритичнаяОшибка, 	" КРИТИЧНАЯ");// КРИТИЧНА

	Возврат КартаСтатусовИУровней;

КонецФункции

#КонецОбласти

Процедура ПриСозданииОбъекта(Знач ПВыводитьВЦвете = Истина, 
							Знач ПИспользоватьПрефиксы = Истина, 
							Знач ПВыводитьДату = Истина)
	
	КонсольВывода = Новый Консоль();
	ИспользоватьПрефиксы = ПИспользоватьПрефиксы;
	ВывестиВЦвете = ПВыводитьВЦвете;
	ВыводитьДату = ПВыводитьДату;

	ЦветоваяСхема = ЦветоваяСхемаПоУмолчанию();
	КартаУровней = КартаУровнейПоУмолчанию();
	
	УстановитьФорматДатыСобытия("[yyyy/MM/dd hh:mm:ss]");
	
КонецПроцедуры

